apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'provided-base'

buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.2'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:1.12.+'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

group = 'com.antwerkz.lariat'
version = '0.1-SNAPSHOT'

test {
    useTestNG()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    provided 'org.slf4j:slf4j-api:1.7.5'
    provided 'org.mongodb.morphia:morphia:0.109'

    testCompile 'com.jayway.awaitility:awaitility:1.6.1'
    testCompile 'org.testng:testng:6.8.1'
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

modifyPom {
    project {
        name 'lariat'
        description 'Morphia extension that transparently archives old versions of entities allowing for rollbacks'
        url 'https://github.com/evanchooly/lariat'
        inceptionYear '2014'

        scm {
            url 'https://github.com/evanchooly/lariat'
            connection 'scm:https://github.com/evanchooly/lariat.git'
            developerConnection 'scm:git@github.com:evanchooly/lariat.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'evanchooly'
                name 'Justin Lee'
                email 'jlee@antwerkz.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
}